#include "../bisim.def".

agent_A_def Proc := 
  Proc = 
  (A\B\S\Kas\T\
   (nu kab\
     outp S (pr A (en (pr T (pr B kab)) Kas)) zero
   )
  ).

agent_B_def Proc :=
  Proc = 
  (A\B\Kbs\T\
   ( 
    inp B (x\
    case x Kbs (m\
      let m (u\ v\ 
       let v (s\ t\
        match A s (
         match T u (outp B B zero)
        )
       )
     )
    )
    )
   )
 ).

agent_S_def Proc :=
 Proc = 
 (A\B\S\Kas\Kbs\T\
 (
  inp S (x\
  let x (x1\x2\
   match A x1 (
   case x2 Kas (m\
   let m (m1\m2\
   let m2 (n1\n2\
   match B n1 (
   match T m1 (
   outp B (en (pr T (pr A n2)) Kbs) zero
 )))))))))).

agent_System_def Proc :=
 agent_S_def ProcS,
 agent_A_def ProcA,
 agent_B_def ProcB, 
 Proc = (A\B\S\T\ 
  (nu kas\ nu kbs\
   par (ProcS A B S kas kbs T) 
   (par (ProcB A B kbs T) (ProcA A B S kas T))
  )
 ).

agent_ASpec_def Proc := 
  Proc = 
  (A\B\S\Kas\T\kab\
     outp S (pr A (en (pr T (pr B kab)) Kas)) zero
  ).

agent_SystemSpec_def Proc :=
 agent_S_def ProcS,
 agent_ASpec_def ProcA,
 agent_B_def ProcB, 
 Proc = (A\B\S\T\ 
  (nu kas\ nu kbs\
   par (ProcS A B S kas kbs T) 
   (par (ProcB A B kbs T) (ProcA A B S kas T (ct m)))
  )
 ).

check_bisim :=
 agent_System_def System, 
 agent_SystemSpec_def SystemSpec,
 H = nil,
 P = (System (ct agent_A) (ct agent_B) (ct agent_S) (ct tt)),
 Q = (SystemSpec (ct agent_A) (ct agent_B) (ct agent_S) (ct tt)),
  println "Checking bisimulation for: ",
  print "- history: ", print_bitrace H, println " ",
  print "- P: ", print_proc P, println " ",
  print "- Q: ", print_proc Q, println " ", 
 bisim H P Q.

